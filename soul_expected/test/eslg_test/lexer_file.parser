// =================================
// Copyright (c) 2025 Seppo Laakko
// Distributed under the MIT license
// =================================

export module soul_expected.slg.lexer.file.parser;

[interface]import soul_expected.ast.slg;
[interface]import soul_expected.ast.cpp;
[implementation]import soul_expected.cpp.token;
[implementation]import soul_expected.cpp.op.token;
[implementation]import soul_expected.punctuation.token;
[implementation]import soul_expected.tool.token;
[implementation]import soul_expected.common.common.parser;
[implementation]import soul_expected.common.token.parser;
[implementation]import soul_expected.cpp.statement.parser;
[implementation]import soul_expected.cpp.declarator.parser;
[implementation]import soul_expected.ast.cpp;
[implementation]import soul_expected.ast.common;
[implementation]import soul_expected.lex.slg;

parser LexerFileParser
{
    lexer soul_expected::lex::slg::SlgLexer<char32_t>;

    main;
    using CommonParser.QualifiedId;
    using CommonParser.ExportModule;
    using CommonParser.Import;
    using CommonParser.ExprString;
    using CommonParser.LexerKeyword;
    using StatementParser.CompoundStatement;
    using DeclaratorParser.TypeId;

    LexerFile(soul_expected::ast::cpp::Context* context, var std::unique_ptr<soul_expected::ast::slg::LexerFile> lexerFile, 
        var std::unique_ptr<soul_expected::ast::slg::Lexer> lxr) : soul_expected::ast::slg::LexerFile*
        ::= 
        (
            empty{ lexerFile.reset(new soul_expected::ast::slg::LexerFile(lexer.FileName())); }
            ExportModule:exportModule{ lexerFile->SetExportModule(exportModule); }
            Imports(lexerFile.get()):imports
            LexerKeyword:lexerKeyword
            ID
            { 
                auto rv = lexer.TokenToUtf8(pos);
                if (!rv) return std::unexpected<int>(rv.error());
                lxr.reset(new soul_expected::ast::slg::Lexer(*rv)); 
            }
            LBRACE
            (
                LexerContent(context, lxr.get()):content
            )*
            RBRACE
        )
        {
            lexerFile->SetLexer(lxr.release());
            return lexerFile.release();
        }
        ;

    Imports(soul_expected::ast::slg::LexerFile* lexerFile)
        ::= (Import:imp{ lexerFile->AddImport(imp); })*
        ;

    LexerContent(soul_expected::ast::cpp::Context* context, soul_expected::ast::slg::Lexer* lxr)
        ::= 
        (   Rules(context, lxr):rls
        |   Variables(context, lxr):vars
        |   Actions(context, lxr):actns
        )
        ;

    Rules(soul_expected::ast::cpp::Context* context, soul_expected::ast::slg::Lexer* lxr)
        ::= 
        (
            RULES
            LBRACE
            (
                Rule(context):rl{ lxr->AddRule(rl); }
            )*
            RBRACE
        )
        ;

    Rule(soul_expected::ast::cpp::Context* context, var int line) : soul_expected::ast::slg::Rule*
        ::= 
        (
            ExprString:expr
            { 
                auto t = lexer.GetToken(pos);
                if (!t) return std::unexpected<int>(t.error());
                auto token = *t;
                line = token->line; 
            }
            Action(context):action
            CompoundStatement(context):code
        )
        {
            return new soul_expected::ast::slg::Rule(expr, code, action, line);
        }
        ;

    Variables(soul_expected::ast::cpp::Context* context, soul_expected::ast::slg::Lexer* lxr)
        ::= 
        (
            VARIABLES
            LBRACE
            (
                Variable(context):variable{ lxr->AddVariable(variable); }
            )*
            RBRACE
        )
        ;

    Variable(soul_expected::ast::cpp::Context* context, var std::string name) : soul_expected::ast::slg::Variable*
        ::= 
        (
            TypeId(context):type 
            ID
            { 
                auto rv = lexer.TokenToUtf8(pos);
                if (!rv) return std::unexpected<int>(rv.error());
                name = *rv;
            }
            SEMICOLON
        )
        {
            return new soul_expected::ast::slg::Variable(type, name);
        }
        ;
		
    Actions(soul_expected::ast::cpp::Context* context, soul_expected::ast::slg::Lexer* lxr, var int actionId)
        ::= 
        (
            ACTIONS
            LBRACE
            (
                Action(context):action{ actionId = action; pass = actionId != -1; } 
                ASSIGN CompoundStatement(context):stmt{ lxr->AddAction(new soul_expected::ast::slg::Action(actionId, stmt)); }
            )*
            RBRACE
        )
        ;

    Action(soul_expected::ast::cpp::Context* context, var int actionId) : int
        ::= DOLLAR 
            LPAREN 
            INTEGER_LITERAL
            { 
                auto rv = lexer.TokenToInt(pos);
                if (!rv) return std::unexpected<int>(rv.error());
                actionId = *rv;
            } 
            RPAREN
            { 
                return actionId; 
            }
        |   empty{ return -1; }
        ;
}
