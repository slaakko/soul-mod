// =================================
// Copyright (c) 2025 Seppo Laakko
// Distributed under the MIT license
// =================================

export module soul_expected.xml.xpath.parser;

[interface]import soul_expected.xml.xpath.expr;
[implementation]import soul_expected.xml.dom_expected;
[implementation]import soul_expected.xml.xpath.lexer;
[implementation]import soul_expected.xml.xpath.token;
[implementation]import soul_expected.xml.xpath.token.parser;

parser XPathParser
{
    lexer soul_expected::xml::xpath::lexer::XPathLexer<char32_t>;
    main;

    Expr : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            OrExpr:orExpr
        )
        { 
            return orExpr; 
        }
        ;

    OrExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            AndExpr:left{ expr.reset(left); }
            (
                OrKeyword:or_ AndExpr:right{ expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(soul_expected::xml::xpath::expr::Operator::or_, expr.release(), right)); }
            )*
        )
        {
            return expr.release();
        }
        ;

    AndExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::=
        (
            EqualityExpr:left{ expr.reset(left); }
            (
                AndKeyword:and_ EqualityExpr:right{ expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(soul_expected::xml::xpath::expr::Operator::and_, expr.release(), right)); }
            )*
        )
        {
            return expr.release();
        }
        ;

    EqualityExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::=
        (
            RelationalExpr:left{ expr.reset(left); }
            (
                EqualityOp:op RelationalExpr:right!{ expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(op, expr.release(), right)); }
            )*
        )
        {
            return expr.release();
        }
        ;

    RelationalExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            AdditiveExpr:left{ expr.reset(left); }
            (
                RelationalOp:op AdditiveExpr:right!{ expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(op, expr.release(), right)); }
            )*
        )
        {
            return expr.release();
        }
        ;

    AdditiveExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            MultiplicativeExpr:left{ expr.reset(left); } 
            (
                AdditiveOp:op MultiplicativeExpr:right!{ expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(op, expr.release(), right)); }
            )*
        )
        {
            return expr.release();
        }
        ;

    MultiplicativeExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            UnaryExpr:left{ expr.reset(left); }
            (
                MultiplicativeOp:op UnaryExpr:right!{ expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(op, expr.release(), right)); }
            )*
        )
        {
            return expr.release();
        }
        ;

    UnaryExpr : soul_expected::xml::xpath::expr::Expr*
        ::= MINUS UnaryExpr:subject{ return new soul_expected::xml::xpath::expr::UnaryExpr(soul_expected::xml::xpath::expr::Operator::minus, subject); }
        |   UnionExpr:unionExpr{ return unionExpr; }
        ;

    UnionExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            PathExpr:left{ expr.reset(left); }
            (
                UNION PathExpr:right{ expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(soul_expected::xml::xpath::expr::Operator::union_, expr.release(), right)); }
            )*
        )
        {
            return expr.release();
        }
        ;

    PathExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            (
                LocationPath:locationPath - FunctionCall:functionCall
            )
            { 
                return locationPath; 
            }
        )
        |   
        (   
            FilterExpr:filterExpr{ expr.reset(filterExpr); }
            (
                (
                    (
                        CombinePathOp:op
                        RelativeLocationPath:right
                    )
                    {
                        switch (op)
                        {
                            case soul_expected::xml::xpath::expr::Operator::slash:
                            {
                                return new soul_expected::xml::xpath::expr::BinaryExpr(soul_expected::xml::xpath::expr::Operator::slash, expr.release(), right);
                            }
                            case soul_expected::xml::xpath::expr::Operator::slashSlash:
                            {
                                return soul_expected::xml::xpath::expr::MakeSlashSlashExpr(expr.release(), right);
                            }
                        }
                    }
                )?
            )
            {
                return expr.release();
            }
        )
        ;

    LocationPath : soul_expected::xml::xpath::expr::Expr*
        ::= AbsoluteLocationPath:absoluteLocationPath{ return absoluteLocationPath; }
        |   RelativeLocationPath:relativeLocationPath{ return relativeLocationPath; }
        ;

    AbsoluteLocationPath(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (   AbbreviatedAbsoluteLocationPath:abbreviatedAbsoluteLocationPath{ expr.reset(abbreviatedAbsoluteLocationPath); }
        |   SLASH{ expr.reset(new soul_expected::xml::xpath::expr::Root()); } 
            (
                RelativeLocationPath:right{ expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(soul_expected::xml::xpath::expr::Operator::slash, expr.release(), right)); }
            )?
        )
        {
            return expr.release();
        }
        ;

    AbbreviatedAbsoluteLocationPath : soul_expected::xml::xpath::expr::Expr*
        ::= SLASH_SLASH RelativeLocationPath:right{ return soul_expected::xml::xpath::expr::MakeSlashSlashExpr(new soul_expected::xml::xpath::expr::Root(), right); }
        ;

    RelativeLocationPath(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            Step:left{ expr.reset(left); }
            (
                (
                    CombinePathOp:op
                    Step:right
                )
                {
                    switch (op)
                    {
                        case soul_expected::xml::xpath::expr::Operator::slash:
                        {
                            expr.reset(new soul_expected::xml::xpath::expr::BinaryExpr(soul_expected::xml::xpath::expr::Operator::slash, expr.release(), right));
                            break;
                        }
                        case soul_expected::xml::xpath::expr::Operator::slashSlash:
                        {
                            expr.reset(soul_expected::xml::xpath::expr::MakeSlashSlashExpr(expr.release(), right));
                            break;
                        }
                    }
                }
            )*
        )
        {
            return expr.release();
        }
        ;

    Step(var std::unique_ptr<soul_expected::xml::xpath::expr::LocationStepExpr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            AxisSpecifier:axis NodeTest:nodeTest{ expr.reset(new soul_expected::xml::xpath::expr::LocationStepExpr(axis, nodeTest)); }
            (
                Predicate:predicate{ expr->AddPredicate(predicate); }
            )*
        |   AbbreviatedStep:abbreviatedStep{ expr.reset(abbreviatedStep); }
        )
        {
            return expr.release();
        }
        ;

    AxisSpecifier : soul_expected::xml::Axis
        ::= AxisName:axis COLON_COLON{ return axis;}
        |   AbbreviatedAxisSpecifier:abbreviatedAxisSpecifier{ return abbreviatedAxisSpecifier; }
        ;

    AxisName : soul_expected::xml::Axis
        ::= NAME
        {
            auto t = lexer.GetToken(pos);
            if (!t) return std::unexpected<int>(t.error());
            auto token = *t;
            switch (lexer.GetKeywordToken(token->match))
            {
                case ANCESTOR:
                {
                    return soul_expected::xml::Axis::ancestor;
                }
                case ANCESTOR_OR_SELF:
                {
                    return soul_expected::xml::Axis::ancestorOrSelf;
                }
                case ATTRIBUTE:
                {
                    return soul_expected::xml::Axis::attribute;
                }
                case CHILD:
                {
                    return soul_expected::xml::Axis::child;
                }
                case DESCENDANT:
                {
                    return soul_expected::xml::Axis::descendant;
                }
                case DESCENDANT_OR_SELF:
                {
                    return soul_expected::xml::Axis::descendantOrSelf;
                }
                case FOLLOWING:
                {
                    return soul_expected::xml::Axis::following;
                }
                case FOLLOWING_SIBLING:
                {
                    return soul_expected::xml::Axis::followingSibling;
                }
                case NAMESPACE:
                {
                    return soul_expected::xml::Axis::ns;
                }
                case PARENT:
                {
                    return soul_expected::xml::Axis::parent;
                }
                case PRECEDING:
                {
                    return soul_expected::xml::Axis::preceding;
                }
                case PRECEDING_SIBLING:
                {
                    return soul_expected::xml::Axis::precedingSibling;
                }
                case SELF:
                {
                    return soul_expected::xml::Axis::self;
                }
                default:
                {
                    pass = false;
                    break;
                }
            }
        }
        ;

    AbbreviatedAxisSpecifier : soul_expected::xml::Axis
        ::= AT{ return soul_expected::xml::Axis::attribute; }
        |   empty{ return soul_expected::xml::Axis::child; }
        ;

    NodeTest : soul_expected::xml::xpath::expr::NodeTest*
        ::= 
        (   
            (
                ProcessingInstructionKeyword:pikw LPAREN Literal:pi RPAREN
            )
            { 
                return new soul_expected::xml::xpath::expr::PILiteralNodeTest(pi); 
            }
        |   
            (
                NodeType:nodeType LPAREN RPAREN
            )
            { 
                return nodeType; 
            }
        |   
            (
                NameTest:nameTest
            )
            {  
                return nameTest; 
            }
        )
        ;

    Literal : soul_expected::xml::xpath::expr::Literal*
        ::= DQ_STRING
            { 
                auto t = lexer.GetToken(pos);
                if (!t) return std::unexpected<int>(t.error());
                auto s = soul_expected::xml::xpath::token::parser::ParseDQString(lexer.FileName(), *t);
                if (!s) return std::unexpected<int>(s.error());
                return new soul_expected::xml::xpath::expr::Literal(*s); 
            }
        |   SQ_STRING
            { 
                auto t = lexer.GetToken(pos);
                if (!t) return std::unexpected<int>(t.error());
                auto s = soul_expected::xml::xpath::token::parser::ParseSQString(lexer.FileName(), *t);
                if (!s) return std::unexpected<int>(s.error());
                return new soul_expected::xml::xpath::expr::Literal(*s); 
            }
        ;

    NodeType : soul_expected::xml::xpath::expr::NodeTest*
        ::= NAME
        {
            auto t = lexer.GetToken(pos);
            if (!t) return std::unexpected<int>(t.error());
            auto token = *t;
            switch (lexer.GetKeywordToken(token->match))
            {
                case COMMENT: 
                {
                    return new soul_expected::xml::xpath::expr::NodeTest(soul_expected::xml::xpath::expr::NodeTestKind::commentNodeTest);
                }
                case TEXT:
                {
                    return new soul_expected::xml::xpath::expr::NodeTest(soul_expected::xml::xpath::expr::NodeTestKind::textNodeTest);
                }
                case PROCESSING_INSTRUCTION:
                {
                    return new soul_expected::xml::xpath::expr::NodeTest(soul_expected::xml::xpath::expr::NodeTestKind::piNodeTest);
                }
                case NODE:
                {
                    return new soul_expected::xml::xpath::expr::NodeTest(soul_expected::xml::xpath::expr::NodeTestKind::anyNodeTest);
                }
                default:
                {
                    pass = false;
                    break;
                }
            }
        }
        ;

    NameTest : soul_expected::xml::xpath::expr::NodeTest*
        ::= STAR{ return new soul_expected::xml::xpath::expr::NodeTest(soul_expected::xml::xpath::expr::NodeTestKind::principalNodeTest); }
        |   NCName:ncname COLON STAR{ return new soul_expected::xml::xpath::expr::PrefixNodeTest(ncname); }
        |   QName:qname{ return new soul_expected::xml::xpath::expr::NameNodeTest(qname); }
        ;

    NCName : std::string
        ::= NAME
            { 
                auto u = lexer.TokenToUtf8(pos);
                if (!u) return std::unexpected<int>(u.error());
                return *u; 
            }
        ;

    QName : std::string
        ::= PrefixedName:prefixedName{ return prefixedName; }
        |   UnprefixedName:unprefixedName{ return unprefixedName; }
        ;

    PrefixedName : std::string
        ::= 
        (
            Prefix:prefix COLON LocalPart:localPart
        )
        {
            return prefix + ":" + localPart;
        }
        ;

    Prefix : std::string
        ::= NCName:ncname{ return ncname; }
        ;

    UnprefixedName : std::string
        ::= LocalPart:localPart{ return localPart; }
        ;

    LocalPart : std::string
        ::= NCName:ncname{ return ncname; }
        ;

    Predicate : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            LBRACKET 
            Expr:expr 
            RBRACKET
        )
        { 
            return expr; 
        }
        ;

    AbbreviatedStep : soul_expected::xml::xpath::expr::LocationStepExpr*
        ::= DOT_DOT
        { 
            return new soul_expected::xml::xpath::expr::LocationStepExpr(soul_expected::xml::Axis::parent, new soul_expected::xml::xpath::expr::NodeTest(soul_expected::xml::xpath::expr::NodeTestKind::anyNodeTest)); 
        }
        |   DOT
        { 
            return new soul_expected::xml::xpath::expr::LocationStepExpr(soul_expected::xml::Axis::self, new soul_expected::xml::xpath::expr::NodeTest(soul_expected::xml::xpath::expr::NodeTestKind::anyNodeTest)); 
        }
        ;

    FunctionCall(var std::unique_ptr<soul_expected::xml::xpath::expr::FunctionCall> functionCall) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            FunctionName:functionName
            LPAREN{ functionCall.reset(new soul_expected::xml::xpath::expr::FunctionCall(functionName)); }
            (Argument:arg{ functionCall->AddArgument(arg); } % COMMA)?
            RPAREN
        )
        {
            return functionCall.release();
        }
        ;

    FunctionName : std::string
        ::= (QName:qname - NodeType:nodeType){ return qname; }
        ;

    Argument : soul_expected::xml::xpath::expr::Expr*
        ::= Expr:expr{ return expr; }
        ;

    FilterExpr(var std::unique_ptr<soul_expected::xml::xpath::expr::Expr> expr) : soul_expected::xml::xpath::expr::Expr*
        ::= 
        (
            PrimaryExpr:primaryExpr{ expr.reset(primaryExpr); } 
            (
                Predicate:predicate{ expr.reset(new soul_expected::xml::xpath::expr::FilterExpr(expr.release(), predicate)); }
            )*
        )
        {
            return expr.release();
        }
        ;

    PrimaryExpr : soul_expected::xml::xpath::expr::Expr*
        ::= FunctionCall:functionCall{ return functionCall; }
        |   VariableReference:variableReference{ return variableReference; }
        |   (LPAREN Expr:expr RPAREN){ return new soul_expected::xml::xpath::expr::UnaryExpr(soul_expected::xml::xpath::expr::Operator::parens, expr); }
        |   Literal:literal{ return literal; }
        |   Number:number{ return number; }
        ;

    VariableReference : soul_expected::xml::xpath::expr::Expr*
        ::= (DOLLAR QName:qname){ return new soul_expected::xml::xpath::expr::VariableReference(qname); }
        ;

    Number : soul_expected::xml::xpath::expr::Expr*
        ::= NUMBER
            { 
                auto d = lexer.TokenToDouble(pos);
                if (!d) return std::unexpected<int>(d.error());
                double number = *d;
                return new soul_expected::xml::xpath::expr::NumberExpr(number); 
            }
        ;

    EqualityOp : soul_expected::xml::xpath::expr::Operator
        ::= EQ{ return soul_expected::xml::xpath::expr::Operator::equal; }
        |   NEQ{ return soul_expected::xml::xpath::expr::Operator::notEqual; }
        ;

    RelationalOp : soul_expected::xml::xpath::expr::Operator
        ::= LESS{ return soul_expected::xml::xpath::expr::Operator::less; }
        |   GREATER{ return soul_expected::xml::xpath::expr::Operator::greater; }
        |   LEQ{ return soul_expected::xml::xpath::expr::Operator::lessOrEqual; }
        |   GEQ{ return soul_expected::xml::xpath::expr::Operator::greaterOrEqual; }
        ;

    AdditiveOp : soul_expected::xml::xpath::expr::Operator
        ::= PLUS{ return soul_expected::xml::xpath::expr::Operator::plus; }
        |   MINUS{ return soul_expected::xml::xpath::expr::Operator::minus; }
        ;

    MultiplicativeOp : soul_expected::xml::xpath::expr::Operator
        ::= STAR{ return soul_expected::xml::xpath::expr::Operator::mul; }
        |   DivKeyword:div{ return soul_expected::xml::xpath::expr::Operator::div; }
        |   ModKeyword:mod{ return soul_expected::xml::xpath::expr::Operator::mod; }
        ;

    CombinePathOp : soul_expected::xml::xpath::expr::Operator
        ::= SLASH{ return soul_expected::xml::xpath::expr::Operator::slash; }
        |   SLASH_SLASH{ return soul_expected::xml::xpath::expr::Operator::slashSlash; }
        ;

    OrKeyword 
        ::= NAME
            { 
                auto t = lexer.GetToken(pos);
                if (!t) return std::unexpected<int>(t.error());
                auto token = *t;
                pass = lexer.GetKeywordToken(token->match) == OR; 
            }
        ;

    AndKeyword
        ::= NAME
            { 
                auto t = lexer.GetToken(pos);
                if (!t) return std::unexpected<int>(t.error());
                auto token = *t;
                pass = lexer.GetKeywordToken(token->match) == AND; 
            }
        ;

    DivKeyword
        ::= NAME
            { 
                auto t = lexer.GetToken(pos);
                if (!t) return std::unexpected<int>(t.error());
                auto token = *t;
                pass = lexer.GetKeywordToken(token->match) == DIV; 
            }
        ;

    ModKeyword
        ::= NAME
            { 
                auto t = lexer.GetToken(pos);
                if (!t) return std::unexpected<int>(t.error());
                auto token = *t;
                pass = lexer.GetKeywordToken(token->match) == MOD; 
            }
        ;

    ProcessingInstructionKeyword
        ::= NAME
            { 
                auto t = lexer.GetToken(pos);
                if (!t) return std::unexpected<int>(t.error());
                auto token = *t;
                pass = lexer.GetKeywordToken(token->match) == PROCESSING_INSTRUCTION; 
            }
        ;
}
