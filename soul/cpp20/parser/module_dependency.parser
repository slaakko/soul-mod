// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

export module soul.cpp20.parser.module_dependency;

[interface]import soul.cpp20.symbols.context;
[interface]import soul.cpp20.ast;
[implementation]import soul.ast.source.pos;
[implementation]import soul.cpp20.token;
[implementation]import soul.cpp20.lexer;
[implementation]import soul.cpp20.parser.modules;
[implementation]import soul.cpp20.parser.punctuation;

parser ModuleDependencyParser
{
    lexer soul::cpp20::lexer::Cpp20Lexer<char32_t>;
    main;

    using ModuleParser.ModuleDeclaration;
    using ModuleParser.ImportDeclaration;
    using ModuleParser.ExportKeyword;
    using ModuleParser.ModuleKeyword;
    using PunctuationParser.Semicolon;

    ModuleUnit(soul::cpp20::symbols::Context* context, var soul::ast::SourcePos sourcePos) : soul::cpp20::ast::Node*
        ::= 
        (
            (
                GlobalModuleFragment(context):globalModuleFragment{ sourcePos = lexer.GetSourcePos(pos); }
            )? 
            ModuleDeclaration(context):moduleDeclaration{ if (!sourcePos.IsValid()) sourcePos = lexer.GetSourcePos(pos); } 
            DeclarationSeq(context):declarations? 
        )
        {
            lexer.MoveToEnd();
            return new soul::cpp20::ast::ModuleUnitNode(sourcePos, globalModuleFragment, moduleDeclaration, declarations, nullptr);
        }
        ;

    GlobalModuleFragment(soul::cpp20::symbols::Context* context, var soul::ast::SourcePos sourcePos) : soul::cpp20::ast::Node*
        ::= 
        (
            ModuleKeyword(context):moduleKw{ sourcePos = lexer.GetSourcePos(pos); }
            Semicolon:semicolon 
        )
        {
            return new soul::cpp20::ast::GlobalModuleFragmentNode(sourcePos, moduleKw, semicolon, nullptr);
        }
        ;

    DeclarationSeq(soul::cpp20::symbols::Context* context, var std::unique_ptr<soul::cpp20::ast::Node> sequence, var std::unique_ptr<soul::cpp20::ast::Node> node) : 
        soul::cpp20::ast::Node*
        ::= 
        (
            Declaration(context):first
            { 
                sequence.reset(new soul::cpp20::ast::DeclarationSequenceNode(lexer.GetSourcePos(pos))); 
                node.reset(first);
                if (node)
                {
                    sequence->AddNode(node.release());
                }
            }
            (
                Declaration(context):next
                { 
                    node.reset(next);
                    if (node)
                    {
                        sequence->AddNode(node.release()); 
                    }
                }
            )*
        )
        {
            return sequence.release();
        }
        ;

    Declaration(soul::cpp20::symbols::Context* context) : soul::cpp20::ast::Node*
        ::= ImportDeclaration(context):moduleImportDeclaration{ return moduleImportDeclaration; }
        |   ExportImportDeclaration(context):exportImportDeclaration{ return exportImportDeclaration; }
        ;

    ExportImportDeclaration(soul::cpp20::symbols::Context* context, var soul::ast::SourcePos sourcePos) : soul::cpp20::ast::Node*
        ::= 
        (
            ExportKeyword(context):exprt{ sourcePos = lexer.GetSourcePos(pos); }
            ImportDeclaration(context):imprt
        )
        {
            return new soul::cpp20::ast::ExportDeclarationNode(sourcePos, exprt, imprt, soul::ast::SourcePos(), soul::ast::SourcePos());
        }
        ;
}
