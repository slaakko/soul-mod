// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

export module soul.slg.expression.file.par;

[interface]import soul.ast.slg;
[implementation]import soul.cpp.token;
[implementation]import soul.cpp.op.token;
[implementation]import soul.punctuation.token;
[implementation]import soul.tool.token;
[implementation]import soul.slg.common.par;
[implementation]import soul.slg.token.par;
[implementation]import soul.lex.slg;

parser ExpressionFileParser
{
    uselexer soul::lex::slg::SlgLexer<char32_t>;

    main;
    using CommonParser.QualifiedId;
    using CommonParser.ExprString;

    ExpressionFile(var std::unique_ptr<soul::ast::slg::ExpressionFile> expressionFile, var std::unique_ptr<soul::ast::slg::ExpressionCollection> expressionCollection) :
        soul::ast::slg::ExpressionFile*
        ::=
        (
            empty{ expressionFile.reset(new soul::ast::slg::ExpressionFile(lexer.FileName())); }
            EXPRESSIONS
            QualifiedId:expressionCollectionName!{ expressionCollection.reset(new soul::ast::slg::ExpressionCollection(expressionCollectionName)); }
            LBRACE! 
            (Expression:expression{ expressionCollection->AddExpression(expression); } % COMMA)?
            RBRACE!
        )
        {
            expressionFile->SetExpressionCollection(expressionCollection.release());
            return expressionFile.release();
        }
        ;

    Expression(var std::string name, var int line) : soul::ast::slg::Expression*
        ::= 
        (
            ID{ auto token = lexer.GetToken(pos); name = util::ToUtf8(token.ToString()); line = token.line; } ASSIGN! ExprString:exprString! SEMICOLON! 
        )
        {
            return new soul::ast::slg::Expression(name, exprString, line);
        }
        ;
}
