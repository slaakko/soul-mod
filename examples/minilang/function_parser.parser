export module minilang.parser.function;

[interface]import minilang.ast;
[implementation]import minilang.lexer;
[implementation]import minilang.token;
[implementation]import minilang.parser.type;
[implementation]import minilang.parser.identifier;
[implementation]import minilang.parser.statement;

parser FunctionParser
{
    lexer minilang::lexer::MinilangLexer<char32_t>;

    using TypeParser.Type;
    using IdentifierParser.Identifier;
    using StatementParser.CompoundStatement;

    Function(var std::unique_ptr<minilang::ast::FunctionNode> function) : minilang::ast::FunctionNode*
        ::= 
		(
			Type:returnType 
            Identifier:functionName! 
            LPAREN!{ function.reset(new minilang::ast::FunctionNode(returnType, functionName)); } 
			ParameterList(function.get()):params? 
            RPAREN! 
			CompoundStatement:functionBody!{ function->SetBody(functionBody); }
		)
		{
			return function.release();
		}
        ;

    ParameterList(minilang::ast::FunctionNode* function)
        ::= 
        (
            Parameter:left{ function->AddParam(left); } 
            (
                COMMA Parameter:right!{ function->AddParam(right); }
            )*
        )
        ;

    Parameter : minilang::ast::ParameterNode*
        ::= 
        (
            Type:type Identifier:name
        )
		{
			return new minilang::ast::ParameterNode(type, name);
		}
        ;
}
