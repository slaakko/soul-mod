// =================================
// Copyright (c) 2023 Seppo Laakko
// Distributed under the MIT license
// =================================

export module otava.parser.concepts;

[interface]import otava.symbols.context;
[implementation]import soul.ast.source.pos;
[implementation]import otava.ast;
[implementation]import otava.token;
[implementation]import otava.lexer;
[implementation]import otava.symbols;
[implementation]import otava.parser.type;
[implementation]import otava.parser.expression;
[implementation]import otava.parser.function;
[implementation]import otava.parser.identifier;
[implementation]import otava.parser.punctuation;
[implementation]import otava.parser.templates;

parser ConceptParser
{
    lexer otava::lexer::OtavaLexer<char32_t>;

    using TypeParser.TypeName;
    using ExpressionParser.Expression;
    using ExpressionParser.LogicalOrExpression;
    using ExpressionParser.PrimaryExpression;
    using FunctionParser.Noexcept;
    using FunctionParser.ParameterDeclarationClause;
    using IdentifierParser.Identifier;
    using IdentifierParser.NestedNameSpecifier;
    using PunctuationParser.Assign;
    using PunctuationParser.Semicolon;
    using TemplateParser.TemplateArgumentList;

    ConceptDefinition(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            CONCEPT{ sourcePos = lexer.GetSourcePos(pos); } 
            ConceptName(context):conceptName 
            Assign:assign{ context->PushSetFlag(otava::symbols::ContextFlags::parsingConceptDefinition); }
            ConstraintExpression(context):constraintExpr{ context->PopFlags(); } / { context->PopFlags(); }
            Semicolon:semicolon
        )
        {
            otava::ast::ConceptDefinitionNode* conceptDefinitionNode = new otava::ast::ConceptDefinitionNode(sourcePos, conceptName, assign, constraintExpr, semicolon);
            otava::symbols::ProcessConcept(conceptDefinitionNode, context);
            return conceptDefinitionNode;
        }
        ;
    
    ConceptName(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> identifierNode) : otava::ast::Node*
        ::= Identifier(context):identifier{ return identifier; }
        ;

    ConceptNameChecked(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> identifierNode) : otava::ast::Node*
        ::= Identifier(context):identifier
            { 
                identifierNode.reset(identifier);
                pass = otava::symbols::IsConceptName(identifierNode.get(), context);
                if (pass)
                {
                    return identifierNode.release(); 
                }
            }
        ;

    ConstraintExpression(otava::symbols::Context* context) : otava::ast::Node*
        ::= LogicalOrExpression(context):logicalOrExpr{ return logicalOrExpr; }
        ;

    RequiresClause(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            REQUIRES{ sourcePos = lexer.GetSourcePos(pos); }
            ConstraintLogicalOrExpression(context):orExpr
        )
        {
            return new otava::ast::RequiresClauseNode(sourcePos, orExpr);
        }
        ;

    ConstraintLogicalOrExpression(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> node, 
        var soul::ast::SourcePos sourcePos, var soul::ast::SourcePos opPos) : otava::ast::Node*
        ::= 
        (
            ConstraintLogicalAndExpression(context):left{ node.reset(left); sourcePos = lexer.GetSourcePos(pos); }
            (
                OR_OR{ opPos = lexer.GetSourcePos(pos); } 
                ConstraintLogicalAndExpression(context):right{ node.reset(new otava::ast::BinaryExprNode(sourcePos, new otava::ast::DisjunctionNode(opPos), node.release(), right)); }
            )*
        )
        {
            return node.release();
        }
        ;

    ConstraintLogicalAndExpression(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> node, 
        var soul::ast::SourcePos sourcePos, var soul::ast::SourcePos opPos) : otava::ast::Node*
        ::= 
        (
            PrimaryExpression(context):left{ node.reset(left); }
            (
                AMP_AMP{ opPos = lexer.GetSourcePos(pos); } 
                PrimaryExpression(context):right{ node.reset(new otava::ast::BinaryExprNode(sourcePos, new otava::ast::ConjunctionNode(opPos), node.release(), right)); }
            )*
        )
        {
            return node.release();
        }
        ;


    RequiresExpression(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            REQUIRES{ sourcePos = lexer.GetSourcePos(pos); } RequirementParameterList(context):params? RequirementBody(context):body
        )
        {
            return new otava::ast::RequiresExprNode(sourcePos, params, body);
        }
        ;

    RequirementParameterList(otava::symbols::Context* context, var std::unique_ptr<otava::ast::ParameterListNode> node, 
        var soul::ast::SourcePos lpPos, var soul::ast::SourcePos rpPos) : otava::ast::Node*
        ::=
        (
            LPAREN{ lpPos = lexer.GetSourcePos(pos); node.reset(new otava::ast::ParameterListNode(lpPos)); } 
            ParameterDeclarationClause(context, node.get()):paramDeclClause 
            RPAREN{ rpPos = lexer.GetSourcePos(pos); }
        )
        {
            node->SetLParenPos(lpPos);
            node->SetRParenPos(rpPos);
            return node.release();
        }
        ;

    RequirementBody(otava::symbols::Context* context, var std::unique_ptr<otava::ast::RequirementBodyNode> node, 
        var soul::ast::SourcePos lbPos, var soul::ast::SourcePos rbPos) : otava::ast::Node*
        ::= 
        (
            LBRACE{ lbPos = lexer.GetSourcePos(pos); node.reset(new otava::ast::RequirementBodyNode(lbPos)); }
            RequirementSeq(context, node.get()):requirementSeq
            RBRACE{ rbPos = lexer.GetSourcePos(pos); }
        )
        {
            node->SetLBracePos(lbPos);
            node->SetRBracePos(rbPos);
            return node.release();
        }
        ;

    RequirementSeq(otava::symbols::Context* context, otava::ast::Node* requirementBody) 
        ::= 
        (
            Requirement(context):requirement{ requirementBody->AddNode(requirement); }
        )+
        ;

    Requirement(otava::symbols::Context* context) : otava::ast::Node*
        ::= NestedRequirement(context):nestedRequirement{ return nestedRequirement; }
        |   TypeRequirement(context):typeRequirement{ return typeRequirement; }
        |   CompoundRequirement(context):compoundRequirement{ return compoundRequirement; }
        |   SimpleRequirement(context):simpleRequirement{ return simpleRequirement; }
        ;

    SimpleRequirement(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= Expression(context):expr{ sourcePos = lexer.GetSourcePos(pos); } Semicolon:semicolon{ return new otava::ast::SimpleRequirementNode(sourcePos, expr, semicolon); }
        ;

    TypeRequirement(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= TYPENAME{ sourcePos = lexer.GetSourcePos(pos); } 
            NestedNameSpecifier(context):nns? 
            empty{ context->PushSetFlag(otava::symbols::ContextFlags::assumeType); }
            TypeName(context):typeName{ context->PopFlags(); } / { context->PopFlags(); }
            Semicolon:semicolon
        { 
            return new otava::ast::TypeRequirementNode(sourcePos, nns, typeName, semicolon); 
        }
        ;

    CompoundRequirement(otava::symbols::Context* context, var soul::ast::SourcePos lbPos, var soul::ast::SourcePos rbPos) : otava::ast::Node*
        ::= 
        (
            LBRACE{ lbPos = lexer.GetSourcePos(pos); }
            Expression(context):expr 
            RBRACE{ rbPos = lexer.GetSourcePos(pos); }
            Noexcept:noexcept_? 
            ReturnTypeRequirement(context):returnTypeRequirement?
            Semicolon:semicolon
        )
        {
            return new otava::ast::CompoundRequirementNode(lbPos, expr, noexcept_, returnTypeRequirement, semicolon, lbPos, rbPos);
        }
        ;

    ReturnTypeRequirement(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            ARROW{ sourcePos = lexer.GetSourcePos(pos); } TypeConstraint(context):typeConstraint
        )
        {
            return new otava::ast::ReturnTypeRequirementNode(sourcePos, typeConstraint);
        }
        ;

    NestedRequirement(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            REQUIRES{ sourcePos = lexer.GetSourcePos(pos); } ConstraintExpression(context):constraintExpr Semicolon:semicolon
        )
        {
            return new otava::ast::NestedRequirementNode(sourcePos, constraintExpr, semicolon);
        }
        ;

    TypeConstraint(otava::symbols::Context* context, var std::unique_ptr<otava::ast::TypeConstraintNode> node, var std::unique_ptr<otava::ast::Node> conceptName, 
        var soul::ast::SourcePos sourcePos, var soul::ast::SourcePos laPos, var soul::ast::SourcePos raPos) : otava::ast::Node*
        ::= 
        (
            (
                (   NestedNameSpecifier(context):nns{ sourcePos = lexer.GetSourcePos(pos); } 
                    ConceptNameChecked(context):conceptName1{ conceptName.reset(new otava::ast::QualifiedIdNode(sourcePos, nns, conceptName1)); }
                |   ConceptNameChecked(context):conceptName2{ sourcePos = lexer.GetSourcePos(pos); conceptName.reset(conceptName2); }
                )
                {
                    node.reset(new otava::ast::TypeConstraintNode(sourcePos, conceptName.release())); 
                }
            )
            (
                LANGLE{ laPos = lexer.GetSourcePos(pos); ++vars->langleCount; context->PushSetFlag(otava::symbols::ContextFlags::parsingTemplateId); node->SetHasTemplateArgumentList(); }
                TemplateArgumentList(context, node.get()):templateArgs?
                RANGLE{ raPos = lexer.GetSourcePos(pos); --vars->langleCount; context->PopFlags(); } / { --vars->langleCount; context->PopFlags(); }
            )?
        )
        {
            node->SetLAnglePos(laPos);
            node->SetRAnglePos(raPos);
            return node.release();
        }
    ;
}
