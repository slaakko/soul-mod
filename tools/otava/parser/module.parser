// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

export module otava.parser.modules;

[interface]import otava.symbols.context;
[implementation]import soul.ast.source.pos;
[implementation]import otava.ast;
[implementation]import otava.token;
[implementation]import otava.lexer;
[implementation]import otava.parser.attribute;
[implementation]import otava.parser.classes;
[implementation]import otava.parser.declaration;
[implementation]import otava.parser.identifier;
[implementation]import otava.parser.punctuation;

parser ModuleParser
{
    lexer otava::lexer::OtavaLexer<char32_t>;

    using AttributeParser.AttributeSpecifierSeq;
    using ClassParser.Private;
    using DeclarationParser.DeclarationSeq;
    using DeclarationParser.Declaration;
    using IdentifierParser.Identifier;
    using PunctuationParser.Colon;
    using PunctuationParser.Semicolon;

    ModuleDeclaration(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            (
                ExportKeyword(context):exportKw{ sourcePos = lexer.GetSourcePos(pos); }
            )? 
            ModuleKeyword(context):moduleKw{ if (!sourcePos.IsValid()) sourcePos = lexer.GetSourcePos(pos); }
            ModuleName:moduleName 
            ModulePartition(context):modulePartition? 
            AttributeSpecifierSeq(context):attributes? 
            Semicolon:semicolon
        )
        {
            return new otava::ast::ModuleDeclarationNode(sourcePos, exportKw, moduleKw, moduleName, modulePartition, attributes, semicolon);
        }
        ;

    ModuleName(var std::u32string moduleNameStr, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            ID{ moduleNameStr = lexer.GetToken(pos).ToString(); sourcePos = lexer.GetSourcePos(pos); } 
            (
                DOT ID{ moduleNameStr.append(1, '.').append(lexer.GetToken(pos).ToString()); }
            )*
        )
        {
            return new otava::ast::ModuleNameNode(sourcePos, moduleNameStr);
        }
        ;

    ExportDeclaration(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> node, var soul::ast::SourcePos sourcePos,
        var soul::ast::SourcePos lbPos, var soul::ast::SourcePos rbPos) : otava::ast::Node*
        ::= 
        (
            ExportKeyword(context):exprt{ sourcePos = lexer.GetSourcePos(pos); }
            (
                (
                    LBRACE{ lbPos = lexer.GetSourcePos(pos); }
                    DeclarationSeq(context):declarations?
                    RBRACE{ rbPos = lexer.GetSourcePos(pos); }
                )
                {
                    node.reset(new otava::ast::ExportDeclarationNode(sourcePos, exprt, declarations, lbPos, rbPos));
                }
            |   ImportDeclaration(context):imprt
                {
                    node.reset(new otava::ast::ExportDeclarationNode(sourcePos, exprt, imprt, soul::ast::SourcePos(), soul::ast::SourcePos()));
                }
            |   Declaration(context):declaration
                {
                    node.reset(new otava::ast::ExportDeclarationNode(sourcePos, exprt, declaration, soul::ast::SourcePos(), soul::ast::SourcePos()));
                }
            )
            {
                return node.release();
            }
        )
        ;

    ImportDeclaration(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos, var std::unique_ptr<otava::ast::Node> subject) : otava::ast::Node*
        ::= 
        (
            ImportKeyword(context):imprt{ sourcePos = lexer.GetSourcePos(pos); }
            (   ModuleName:moduleName{ subject.reset(moduleName); }
            |   ModulePartition(context):modulePartition{ subject.reset(modulePartition); }
            |   HeaderName(context):headerName{ subject.reset(headerName); }
            )
            AttributeSpecifierSeq(context):attributes? 
            Semicolon:semicolon
        )
        {
            return new otava::ast::ImportDeclarationNode(sourcePos, imprt, subject.release(), attributes, semicolon);
        }
        ;

    ModulePartition(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            Colon:colon{ sourcePos = lexer.GetSourcePos(pos); }
            ModuleName:moduleName
        )
        {
            return new otava::ast::ModulePartitionNode(sourcePos, moduleName);
        }
        ;    

    ExportKeyword(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            Identifier(context):identifier{ sourcePos = lexer.GetSourcePos(pos); pass = lexer.GetToken(pos).ToString() == U"export"; }
        )
        {
            return new otava::ast::ExportNode(sourcePos);
        }
        ;

    ImportKeyword(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            Identifier(context):identifier{ sourcePos = lexer.GetSourcePos(pos); pass = lexer.GetToken(pos).ToString() == U"import"; }
        )
        {
            return new otava::ast::ImportNode(sourcePos);
        }
        ;

    ModuleKeyword(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            Identifier(context):identifier{ sourcePos = lexer.GetSourcePos(pos); pass = lexer.GetToken(pos).ToString() == U"module"; }
        )
        {
            return new otava::ast::ModuleNode(sourcePos);
        }
        ;

    GlobalModuleFragment(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            ModuleKeyword(context):moduleKw{ sourcePos = lexer.GetSourcePos(pos); }
            Semicolon:semicolon 
            DeclarationSeq(context):declarations?
        )
        {
            return new otava::ast::GlobalModuleFragmentNode(sourcePos, moduleKw, semicolon, declarations);
        }
        ;

    PrivateModuleFragment(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::=
        (
            ModuleKeyword(context):moduleKw{ sourcePos = lexer.GetSourcePos(pos); }
            Colon:colon
            Private:privat
            Semicolon:semicolon
            DeclarationSeq(context):declarations?
        )
        {
            return new otava::ast::PrivateModuleFragmentNode(sourcePos, moduleKw, colon, privat, semicolon, declarations);
        }
        ;

    HeaderName(otava::symbols::Context* context) : Node*
        ::= ANGLE_HEADER_NAME{ return new otava::ast::AngleHeaderName(lexer.GetSourcePos(pos), lexer.GetToken(pos).ToString()); }
        |   QUOTE_HEADER_NAME{ return new otava::ast::QuoteHeaderName(lexer.GetSourcePos(pos), lexer.GetToken(pos).ToString()); }
        ;

}