// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

export module otava.parser.templates;

[interface]import otava.symbols.context;
[implementation]import otava.ast;
[implementation]import otava.token;
[implementation]import otava.lexer;
[implementation]import otava.symbols;
[implementation]import otava.parser.concepts;
[implementation]import otava.parser.declaration;
[implementation]import otava.parser.function;
[implementation]import otava.parser.expression;
[implementation]import otava.parser.identifier;
[implementation]import otava.parser.literal;
[implementation]import otava.parser.punctuation;
[implementation]import otava.parser.type;

parser TemplateParser
{
    lexer otava::lexer::OtavaLexer<char32_t>;

    using ConceptParser.ConceptDefinition;
    using ConceptParser.RequiresClause;
    using ConceptParser.TypeConstraint;
    using DeclarationParser.Declaration;
    using DeclarationParser.ExplicitSpecifier;
    using ExpressionParser.ConstantExpression;
    using ExpressionParser.IdExpression;
    using FunctionParser.OperatorFunctionId;
    using FunctionParser.ParameterDeclaration;
    using FunctionParser.ParameterDeclarationClause;
    using IdentifierParser.Identifier;
    using LiteralParser.LiteralOperatorId;
    using PunctuationParser.Assign;
    using PunctuationParser.Comma;
    using PunctuationParser.Ellipsis;
    using PunctuationParser.Semicolon;
    using TypeParser.TypeId;

    TemplateDeclaration(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> templateHeadNode, 
        var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= TemplateHead(context, true):templateHead{ templateHeadNode.reset(templateHead); sourcePos = lexer.GetSourcePos(pos); }
            (   ConceptDefinition(context):concepDefinition
                { 
                    otava::symbols::EndTemplateDeclaration(templateHeadNode.get(), context); 
                    return new otava::ast::TemplateDeclarationNode(sourcePos, templateHeadNode.release(), concepDefinition); 
                }
            |   Declaration(context):declaration
                { 
                    otava::symbols::EndTemplateDeclaration(templateHeadNode.get(), context); 
                    return new otava::ast::TemplateDeclarationNode(sourcePos, templateHeadNode.release(), declaration); 
                }
            |   empty{ otava::symbols::RemoveTemplateDeclaration(context); pass = false; }
            )
        ;

    TemplateHead(otava::symbols::Context* context, bool begin, var soul::ast::SourcePos sourcePos, var std::unique_ptr<otava::ast::TemplateHeadNode> templateHeadNode) : 
        otava::ast::Node*
        ::= 
        (
            TEMPLATE
            { 
                sourcePos = lexer.GetSourcePos(pos); 
                templateHeadNode.reset(new otava::ast::TemplateHeadNode(sourcePos)); 
                if (begin) 
                {
                    otava::symbols::BeginTemplateDeclaration(templateHeadNode.get(), context); 
                }
            }
            TemplateParameterList(context):templateParamList{ templateHeadNode->SetTemplateParameterList(templateParamList); }
            (RequiresClause(context):requiresClause{ templateHeadNode->SetRequiresClause(requiresClause); })?
        )
        {
            return templateHeadNode.release();
        }
        ;

    TemplateParameterList(otava::symbols::Context* context, var int index, var std::unique_ptr<otava::ast::Node> templateParameterNode, 
        var std::unique_ptr<otava::ast::TemplateParameterListNode> node, var soul::ast::SourcePos laPos, var soul::ast::SourcePos raPos) : otava::ast::Node*
        ::= 
        (
            LANGLE{ soul::ast::SourcePos sourcePos = lexer.GetSourcePos(pos); node.reset(new otava::ast::TemplateParameterListNode(sourcePos)); laPos = sourcePos; } 
            TemplateParameter(context):first
            { 
                templateParameterNode.reset(first); 
                otava::symbols::AddTemplateParameter(templateParameterNode.get(), index, context); 
                node->AddNode(templateParameterNode.release()); 
                index = index + 1; 
            } 
            (
                Comma:comma{ node->AddNode(comma); } 
                TemplateParameter(context):next
                { 
                    templateParameterNode.reset(next); 
                    otava::symbols::AddTemplateParameter(templateParameterNode.get(), index, context); 
                    node->AddNode(templateParameterNode.release()); 
                    index = index + 1; 
                }
            )*
            RANGLE{ raPos = lexer.GetSourcePos(pos); }
        )
        {
            node->SetLAnglePos(laPos);
            node->SetRAnglePos(raPos);
            return node.release();
        }
        ;

    TemplateParameter(otava::symbols::Context* context) : otava::ast::Node*
        ::= TypeParameter(context):typeParam{ return typeParam; }
        |   ParameterDeclaration(context):param{ return param; }
        ;

    TypeParameter(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos, 
        var std::unique_ptr<otava::ast::Node> ellipsisNode, var std::unique_ptr<otava::ast::Node> identifierNode) : otava::ast::Node*
        ::= (TemplateHead(context, false):templateHead{ sourcePos = lexer.GetSourcePos(pos); })?
            (   TypeParameterKey:key{ if (!sourcePos.IsValid()) sourcePos = lexer.GetSourcePos(pos); }
                (   Identifier(context):id 
                    Assign:asgn 
                    (   IdExpression(context):idExpr{ return new otava::ast::TypeParameterNode(sourcePos, key, id, asgn, idExpr, nullptr, templateHead); }
                    |   TypeId(context):typeId{ return new otava::ast::TypeParameterNode(sourcePos, key, id, asgn, typeId, nullptr, templateHead); }
                    )
                |   (Ellipsis:ellipsis? Identifier(context):id2?)
                    {
                        ellipsisNode.reset(ellipsis);
                        identifierNode.reset(id2);
                        if (!ellipsisNode && !identifierNode)
                        {
                            pass = false;
                        }
                        else
                        {
                            return new otava::ast::TypeParameterNode(sourcePos, key, identifierNode.release(), nullptr, nullptr, ellipsisNode.release(), templateHead); 
                        }
                    }
                )
            |   TypeConstraint(context):constraint{ if (!sourcePos.IsValid()) sourcePos = lexer.GetSourcePos(pos); }
                (   Identifier(context):id3 
                    Assign:asgn2 
                    TypeId(context):typeId2{ return new otava::ast::TypeParameterNode(sourcePos, constraint, id3, asgn2, typeId2, nullptr, templateHead); }
                |   (Ellipsis:ellipsis2? Identifier(context):id4?)
                    {
                        ellipsisNode.reset(ellipsis2);
                        identifierNode.reset(id4);
                        if (!ellipsisNode && !identifierNode)
                        {
                            pass = false;
                        }
                        else
                        {
                            return new otava::ast::TypeParameterNode(sourcePos, constraint, identifierNode.release(), nullptr, nullptr, ellipsisNode.release(), templateHead); 
                        }
                    }
                )
            )
        ;

    TypeParameterKey : otava::ast::Node*
        ::= Typename:typename_{ return typename_; }
        |   Class:cls{ return cls; }
        ;

    Typename : otava::ast::Node*
        ::= TYPENAME{ return new otava::ast::TypenameNode(lexer.GetSourcePos(pos)); }
        ;

    Class : otava::ast::Node*
        ::= CLASS{ return new otava::ast::ClassNode(lexer.GetSourcePos(pos)); }
        ;

    TemplateId(otava::symbols::Context* context, var std::unique_ptr<otava::ast::TemplateIdNode> node, var std::unique_ptr<otava::ast::Node> templateName, 
        var soul::ast::SourcePos sourcePos, var soul::ast::SourcePos laPos, var soul::ast::SourcePos raPos) : otava::ast::Node*
        ::= 
        (
            (
                (   OperatorFunctionId(context):operatorFunctionId{ templateName.reset(operatorFunctionId); sourcePos = lexer.GetSourcePos(pos); }
                |   LiteralOperatorId(context):literalOperatorId{ templateName.reset(literalOperatorId); sourcePos = lexer.GetSourcePos(pos); }
                )
                LANGLE
                { 
                    laPos = lexer.GetSourcePos(pos); 
                    ++vars->langleCount; 
                    context->PushSetFlag(otava::symbols::ContextFlags::parsingTemplateId); 
                    node.reset(new otava::ast::TemplateIdNode(sourcePos, templateName.release())); 
                }
                TemplateArgumentList(context, node.get()):templateArgs?
                RANGLE
                { 
                    raPos = lexer.GetSourcePos(pos); 
                    --vars->langleCount; 
                    context->PopFlags(); 
                }
                / 
                { 
                    --vars->langleCount; 
                    context->PopFlags(); 
                }
            )
            {
                node->SetLAnglePos(laPos);
                node->SetRAnglePos(raPos);
                return node.release();
            }
        )
        |   SimpleTemplateId(context):simpleTemplateId{ return simpleTemplateId; }
        ;

    SimpleTemplateId(otava::symbols::Context* context, var std::unique_ptr<otava::ast::TemplateIdNode> node, var soul::ast::SourcePos sourcePos, 
        var soul::ast::SourcePos laPos, var soul::ast::SourcePos raPos) : otava::ast::Node*
        ::= 
        (
            TemplateName(context):templateName{ sourcePos = lexer.GetSourcePos(pos); }
            LANGLE
            { 
                laPos = lexer.GetSourcePos(pos); 
                ++vars->langleCount; 
                context->PushSetFlag(otava::symbols::ContextFlags::parsingTemplateId); 
                node.reset(new otava::ast::TemplateIdNode(sourcePos, templateName)); 
            }
            TemplateArgumentList(context, node.get()):templateArgs?
            RANGLE
            { 
                raPos = lexer.GetSourcePos(pos); 
                --vars->langleCount; 
                context->PopFlags(); 
            }
            / 
            { 
                --vars->langleCount; 
                context->PopFlags(); 
            }
        )
        {
            node->SetLAnglePos(laPos);
            node->SetRAnglePos(raPos);
            return node.release();
        }
        ;

    TemplateName(otava::symbols::Context* context) : otava::ast::Node*
        ::= Identifier(context):identifier{ return identifier; }
        ;

    TemplateArgumentList(otava::symbols::Context* context, otava::ast::Node* templateIdNode, var int index)
        ::= 
        (
            TemplateArgument(context, templateIdNode, index):left{ templateIdNode->AddNode(left); ++index; }
            (
                Comma:comma{ templateIdNode->AddNode(comma); } 
                TemplateArgument(context, templateIdNode, index):right{ templateIdNode->AddNode(right); ++index; }
            )*
        )
        (
            ELLIPSIS{ templateIdNode->AddNode(new otava::ast::EllipsisNode(lexer.GetSourcePos(pos))); }
        )?
        ;

    TemplateArgument(otava::symbols::Context* context, otava::ast::Node* templateIdNode, int index) : otava::ast::Node*
        ::= empty{ pass = otava::symbols::TemplateArgCanBeTypeId(templateIdNode, index); } 
            TypeId(context):typeId 
            TemplateArgNext:next
            { 
                return typeId; 
            }
        |   ConstantExpression(context):constantExpr{ return constantExpr; }
        |   IdExpression(context):idExpr{ return idExpr; }
        ;

    TemplateArgNext
        ::= &(RANGLE | COMMA | ELLIPSIS)
        ;

    DeductionGuide(otava::symbols::Context* context, var std::unique_ptr<otava::ast::ParameterListNode> params, 
        var soul::ast::SourcePos sourcePos, var soul::ast::SourcePos lpPos, var soul::ast::SourcePos rpPos) : otava::ast::Node*
        ::= 
        (
            (ExplicitSpecifier(context):explicitSpecifier{ sourcePos = lexer.GetSourcePos(pos); })?
            TemplateName(context):templateName{ if (!sourcePos.IsValid()) sourcePos = lexer.GetSourcePos(pos); }
            LPAREN{ lpPos = lexer.GetSourcePos(pos); params.reset(new otava::ast::ParameterListNode(lexer.GetSourcePos(pos))); }
            ParameterDeclarationClause(context, params.get()):paramDeclarationClause
            RPAREN{ rpPos = lexer.GetSourcePos(pos); }
            Arrow:arrow 
            SimpleTemplateId(context):templateId
            Semicolon:semicolon
        )
        {
            return new otava::ast::DeductionGuideNode(sourcePos, templateName, params.release(), arrow, templateId, explicitSpecifier, semicolon, lpPos, rpPos);
        }
        ;

    Arrow : otava::ast::Node*
        ::= ARROW{ return new otava::ast::ArrowNode(lexer.GetSourcePos(pos)); }
        ;

    ExplicitInstantiation(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            (Extern:extrn{ sourcePos = lexer.GetSourcePos(pos); })? 
            Template:tmp{ if (!sourcePos.IsValid()) sourcePos = lexer.GetSourcePos(pos); }
            Declaration(context):declaration
        )
        {
            return new otava::ast::ExplicitInstantiationNode(sourcePos, extrn, tmp, declaration);
        }
        ;

    ExplicitSpecialization(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos, var soul::ast::SourcePos laPos, var soul::ast::SourcePos raPos) : 
        otava::ast::Node*
        ::= 
        (
            Template:tmp{ sourcePos = lexer.GetSourcePos(pos); }
            LANGLE{ laPos = lexer.GetSourcePos(pos); }
            RANGLE{ raPos = lexer.GetSourcePos(pos); }
            empty{ context->PushSetFlag(otava::symbols::ContextFlags::parsingTemplateDeclaration); }
            Declaration(context):declaration{ context->PopFlags(); } / { context->PopFlags(); }
        )
        {
            return new otava::ast::ExplicitSpecializationNode(sourcePos, tmp, declaration, laPos, raPos);
        }
        ;

    Extern : otava::ast::Node*
        ::= EXTERN{ return new otava::ast::ExternNode(lexer.GetSourcePos(pos)); }
        ;

    Template : otava::ast::Node*
        ::= TEMPLATE{ return new otava::ast::TemplateNode(lexer.GetSourcePos(pos)); }
        ;

}