// =================================
// Copyright (c) 2025 Seppo Laakko
// Distributed under the MIT license
// =================================

export module otava.parser.attribute;

[interface]import otava.symbols.context;
[interface]import otava.ast;
[implementation]import otava.token;
[implementation]import otava.lexer;
[implementation]import otava.parser.expression;
[implementation]import otava.parser.identifier;
[implementation]import otava.parser.punctuation;
[implementation]import otava.parser.type;

parser AttributeParser
{
    lexer otava::lexer::OtavaLexer<char32_t>;

    using ExpressionParser.ConstantExpression;
    using IdentifierParser.Identifier;
    using PunctuationParser.ColonColon;
    using PunctuationParser.Comma;
    using PunctuationParser.LParen;
    using PunctuationParser.RParen;
    using PunctuationParser.LBracket;
    using PunctuationParser.RBracket;
    using PunctuationParser.LBrace;
    using PunctuationParser.RBrace;
    using PunctuationParser.Ellipsis;
    using TypeParser.TypeId;

    AttributeSpecifierSeq(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> node) : otava::ast::Node*
        ::= 
        (
            AttributeSpecifier(context):first{ node.reset(new otava::ast::AttributeSpecifierSequenceNode(lexer.GetSourcePos(pos))); node->AddNode(first); }
            (
                AttributeSpecifier(context):next{ node->AddNode(next); }
            )*
        )
        {
            return node.release();
        }
        ;

    AttributeSpecifier(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos, 
        var soul::ast::SourcePos lbPos1, var soul::ast::SourcePos lbPos2,
        var soul::ast::SourcePos rbPos1, var soul::ast::SourcePos rbPos2,
        var std::unique_ptr<otava::ast::AttributeSpecifierNode> attributeSpecifierNode) : otava::ast::Node*
        ::= 
        (
            LBRACKET{ lbPos1 = lexer.GetSourcePos(pos); sourcePos = lbPos1; }
            LBRACKET{ lbPos2 = lexer.GetSourcePos(pos); sourcePos = lbPos2; attributeSpecifierNode.reset(new otava::ast::AttributeSpecifierNode(sourcePos)); } 
            (AttributeUsingPrefix(context):usingPrefix{ attributeSpecifierNode->SetUsingPrefix(usingPrefix); })? 
            AttributeList(context, attributeSpecifierNode.get()):attributeList
            RBRACKET{ rbPos1 = lexer.GetSourcePos(pos); } 
            RBRACKET{ rbPos2 = lexer.GetSourcePos(pos); attributeSpecifierNode->SetBracketPositions(lbPos1, lbPos2, rbPos1, rbPos2); return attributeSpecifierNode.release(); } 
        |   AlignmentSpecifier(context):alignmentSpecifier{ return alignmentSpecifier; }
        )
        ;

    AttributeList(otava::symbols::Context* context, otava::ast::Node* container)
        ::= 
        (
            (   Attribute(context):firstOpt{ container->AddNode(firstOpt); }
            )? 
            (   Comma:comma1{ container->AddNode(comma1); } 
                (   Attribute(context):nextOpt{ container->AddNode(nextOpt); }
                )?
            )*
        |   empty{ container->Clear(); }
            Attribute(context):first{ container->AddNode(first); } 
            (   Comma:comma2{ container->AddNode(comma2); } 
                Attribute(context):next{ container->AddNode(next); } 
                Ellipsis:ellipsis{ container->AddNode(ellipsis); }
            )*
        )
        ;

    AttributeUsingPrefix(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos, var soul::ast::SourcePos colonPos) : otava::ast::Node*
        ::= 
        (
            USING{ sourcePos = lexer.GetSourcePos(pos); } AttributeNamespace(context):ns COLON{ colonPos = lexer.GetSourcePos(pos); }
        )
        {
            return new otava::ast::AttributeUsingPrefixNode(sourcePos, ns, colonPos);
        }
        ;

    AttributeNamespace(otava::symbols::Context* context) : otava::ast::Node*
        ::= Identifier(context):ns{ return ns; }
        ;

    Attribute(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            AttributeToken(context):attributeToken{ sourcePos = lexer.GetSourcePos(pos); } AttributeArgumentClause(context):attributeArgs?
        )
        {
            return new otava::ast::AttributeNode(sourcePos, attributeToken, attributeArgs);
        }
        ;

    AttributeToken(otava::symbols::Context* context) : otava::ast::Node*
        ::= AttributeScopedToken(context):scopedToken{ return scopedToken; }
        |   Identifier(context):identifier{ return identifier; }
        ;

    AttributeScopedToken(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos) : otava::ast::Node*
        ::= 
        (
            AttributeNamespace(context):ns{ sourcePos = lexer.GetSourcePos(pos); } ColonColon:colonColon Identifier(context):identifier
        )
        {
            return new otava::ast::AttributeScopedTokenNode(sourcePos, ns, colonColon, identifier);
        }
        ;

    AttributeArgumentClause(otava::symbols::Context* context, var soul::ast::SourcePos sourcePos, var soul::ast::SourcePos lpPos, var soul::ast::SourcePos rpPos) : otava::ast::Node*
        ::= 
        (
            LPAREN{ lpPos = lexer.GetSourcePos(pos); } BalancedTokenSeq(context):balancedTokenSeq? RPAREN{ rpPos = lexer.GetSourcePos(pos); } 
        )
        {
            return new otava::ast::AttributeArgumentsNode(sourcePos, balancedTokenSeq, lpPos, rpPos);
        }
        ;

    BalancedTokenSeq(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> sequence) : otava::ast::Node*
        ::= 
        (
            BalancedToken(context):first{ sequence.reset(new otava::ast::BalancedTokenSequenceNode(lexer.GetSourcePos(pos))); sequence->AddNode(first); }
            (
                BalancedToken(context):next{ sequence->AddNode(next); }
            )*
        )
        {
            return sequence.release();
        }
        ;

    BalancedToken(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> seq) : otava::ast::Node*
        ::= 
        (   LParen:lp{ seq.reset(new otava::ast::BalancedTokenSequenceNode(lexer.GetSourcePos(pos))); seq->AddNode(lp); } 
            BalancedTokenSeq(context):c1{ seq->AddNode(c1); } 
            RParen:rp{ seq->AddNode(rp); }
        |   LBracket:lb{ seq.reset(new otava::ast::BalancedTokenSequenceNode(lexer.GetSourcePos(pos))); seq->AddNode(lb); } 
            BalancedTokenSeq(context):c2{ seq->AddNode(c2); } 
            RBracket:rb{ seq->AddNode(rb); }
        |   LBrace:lbr{ seq.reset(new otava::ast::BalancedTokenSequenceNode(lexer.GetSourcePos(pos))); seq->AddNode(lbr); } 
            BalancedTokenSeq(context):c3{ seq->AddNode(c3); } 
            RBrace:rbr{ seq->AddNode(rbr); }
        |   (any - (LPAREN | RPAREN | LBRACKET | RBRACKET | LBRACE | RBRACE)){ seq.reset(new otava::ast::TokenNode(lexer.GetSourcePos(pos), lexer.GetToken(pos).ToString())); }
        )
        {
            return seq.release();
        }
        ;

    AlignmentSpecifier(otava::symbols::Context* context, var std::unique_ptr<otava::ast::Node> alignment, var soul::ast::SourcePos sourcePos, 
        var soul::ast::SourcePos lpPos, var soul::ast::SourcePos rpPos) : otava::ast::Node*
        ::= 
        (   ALIGNAS{ sourcePos = lexer.GetSourcePos(pos); } 
            LPAREN{ lpPos = lexer.GetSourcePos(pos); }
            (   TypeId(context):typeId{ alignment.reset(typeId); } 
            |   ConstantExpression(context):constantExpr{ alignment.reset(constantExpr); }
            )
            Ellipsis:ellipsis?
            RPAREN{ rpPos = lexer.GetSourcePos(pos); }
        )
        {
            return new otava::ast::AlignmentSpecifierNode(sourcePos, alignment.release(), ellipsis, lpPos, rpPos);
        }
        ;
}
